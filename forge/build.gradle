plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

file("../gradle.properties").withInputStream {
    def props = new Properties()
    props.load(it)
    props.each { prop -> project.ext.set(prop.key, prop.value) }
}

version = mod_version
group = mod_group

base {
    archivesName = "${mod_name}-${minecraft_version}-forge"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

eclipse {
    project { name = "${mod_name}-${minecraft_version}-forge" }
}

minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    mappings channel: 'official', version: minecraft_version

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('../run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_name}" {
                    source sourceSets.main
                }
            }
        }

        client {
        }

        server {
        }
    }
}

sourceSets.main.java { srcDir '../src/main/java' }
sourceSets.main.resources { srcDir '../src/main/resources' }

repositories {
    maven { url "https://maven.shedaniel.me/" }

    maven { url "https://cursemaven.com"}
    maven { url 'https://jitpack.io' }
    maven { url "https://maven.architectury.dev/" }
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"

    // depenendencies

    implementation "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"
    implementation fg.deobf("curse.maven:overgeared-1277786:6955461")

    // for crafts

    implementation fg.deobf("curse.maven:create-328085:6641603")

    // epic knights

    implementation fg.deobf("curse.maven:epic-knights-armor-and-weapons-509041:6028374")
    implementation fg.deobf("curse.maven:epic-knights-addon-925866:6036869")
    implementation fg.deobf("curse.maven:epic-knights-slavic-armory-942000:5458480")
    implementation fg.deobf("curse.maven:epic-knights-antique-legacy-1033634:5496564")
    // implementation fg.deobf("curse.maven:epic-knights-open-helms-1176829:6159457")

    // for testing shit

    runtimeOnly fg.deobf("curse.maven:jei-238222:6600311")
    runtimeOnly fg.deobf("curse.maven:mmmmmmmmmmmm-225738:6860192")
    runtimeOnly fg.deobf("curse.maven:selene-499980:6935940")
    runtimeOnly fg.deobf("curse.maven:workers-523860:${recruits_identifier}")

    runtimeOnly fg.deobf("dev.architectury:architectury-forge:${architectury_api_version}")

    // ./gradlew --console=plain dependencies | grep mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.7:processor'

    implementation "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"
}

sourceSets.main.refMap = 'mixins.musketmod.refmap.json'

mixin {
    add sourceSets.main
    config 'musketmod.mixins.json'

    // debug.verbose = true
    // debug.export = true
    dumpTargetOnFailure = true
}

jar {
    manifest {
        attributes([
                MixinConfigs: 'musketmod.mixins.json',
        ])
    }
}

tasks.named('processResources', ProcessResources).configure {
    filesMatching('META-INF/mods.toml') {
        expand [
                mod_version: mod_version,
        ]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}